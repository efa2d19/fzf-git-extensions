#!/usr/bin/env zsh

local help local_branches

local deps=(
    'git'
    'fzf'
)

local name="${0:t}"
local usage=(
    'Usage:'
    "\t$name [-h|--help]"
    "\t$name [-l|--local] <optional_query>"
)

zparseopts -D -F -- \
    {h,-help}=help \
    {l,-local}=local_branches ||
    { print -l "\n${(@)usage}" && exit 1 }

[[ -z $help ]] || { print -l "${(@)usage}" && exit 0 }

local query=''
[[ -z $@ ]] || { query="$@" }

function check_deps() {
    [[ -n $deps ]] || { echo '[!] Incorrect env - check_deps' && exit 2 }

    local failed
    for dep in "${(@)deps}"; do
        type "$dep" &>/dev/null || { echo '[!] Missing -' "$dep" && failed=1 }
    done

    [[ -z $failed ]] || { exit 1 }
}

function main() {
    check_deps

    local width="$(tput cols)"
    local preview_width="$(( $width / 4 * 3 ))"
    local preview_width_folded="$(( $width / 5 ))"
    local preview_window="right:${preview_width}:border-vertical"

    local bind_fold="change-preview-window(${preview_width_folded}|${preview_width})"

    local git_argv=()
    [[ -n $local_branches ]] || git_argv+=('-a')

    git branch "${(@)git_argv}" |
    sed -E 's/^(\*| ) //' |
    sed -E 's/^\(HEAD detached [a-zA-Z]+ (.+)\)$/\1/' |
    awk '{print $NF}' |
    fzf \
        --cycle \
        --query="$query" \
        --preview='git log --decorate --graph {}' \
        --preview-window="$preview_window" \
        --bind="ctrl-\\:$bind_fold" |
    sed -E 's/^(remotes\/)?(origin\/)?//' |
    xargs git switch
}

main
